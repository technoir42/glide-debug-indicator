plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "com.github.technoir42.aar-publish" version "1.0.2"
    id "maven-publish"
    id "signing"
}

group = "com.github.technoir42"
version = "0.9.1"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.all { variant ->
        variant.generateBuildConfigProvider.configure { enabled = false }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    api "com.github.bumptech.glide:glide:$versions.glide"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.android

            pom {
                name = "Glide Debug Indicator"
                description = "Library for debugging resource caching in Glide."
                url = "https://github.com/technoir42/glide-debug-indicator"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "technoir"
                        name = "Sergey Chelombitko"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/technoir42/glide-debug-indicator.git"
                    developerConnection = "scm:git:ssh://git@github.com/technoir42/glide-debug-indicator.git"
                    url = "https://github.com/technoir42/glide-debug-indicator"
                }
            }
        }
    }

    repositories {
        maven {
            name = "bintray"
            url = "https://api.bintray.com/maven/sch/maven/glide-debug-indicator/;publish=1"
            credentials {
                username = findProperty("BINTRAY_USER")
                password = findProperty("BINTRAY_KEY")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
